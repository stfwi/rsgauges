// @file build.gradle
// Gauges and Switches mod gradle build relay (mc1.14.4)
import net.minecraftforge.gradle.common.task.SignJar
buildscript {
  repositories {
    maven { url = 'https://files.minecraftforge.net/maven' }
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
  }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'
//-----------------------------------------------------------------------------
version = "${version_rsgauges}"
group = "wile.rsgauges"
archivesBaseName = "rsgauges-${version_minecraft}"

repositories {
  maven { name = "Progwml6 maven"; url = "https://dvs1.progwml6.com/files/maven/" } // JEI files
  maven { name = "ModMaven"; url = "modmaven.k-4u.nl" } // JEI files, fallback
}

minecraft {
  mappings channel: 'snapshot', version: "${version_fml_mappings}"
  // accessTransformer = file('build/resources/main/META-INF/accesstransformer.cfg')
  runs {
    client {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
      property 'forge.logging.console.level', 'debug'
      mods {
        rsgauges {
          source sourceSets.main
        }
      }
    }
    server {
      workingDirectory project.file('run')
      property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
      property 'forge.logging.console.level', 'debug'
      mods {
        rsgauges {
          source sourceSets.main
        }
      }
    }
  }
}

dependencies {
  minecraft "net.minecraftforge:forge:${version_forge}"
  compileOnly fg.deobf("mezz.jei:jei-${version_jei}:api")
  runtimeOnly fg.deobf("mezz.jei:jei-${version_jei}")
}

processResources {
  outputs.upToDateWhen { false }
  doLast { file("${sourceSets.main.output.resourcesDir}/.gitversion-rsgauges").text = 'git log "-1" "--format=%h"'.execute().in.text.trim() }
}

jar {
  manifest {
    attributes([
      "Specification-Title": "rsgauges",
      "Specification-Vendor": "wilechaote",
      "Specification-Version": "1", // We are version 1 of ourselves
      "Implementation-Title": project.name,
      "Implementation-Version": "${version_rsgauges}",
      "Implementation-Vendor" :"wilechaote",
      "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])
  }
}

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) { type 'jar'; builtBy 'reobfJar' }

def signing = { ->
  def sp = new Properties()
  sp.keystore_file = project.keystore_file
  sp.keystore_alias = project.keystore_alias
  sp.keystore_pass = project.keystore_pass
  sp.keystore_keypass = project.keystore_keypass
  sp.fingerprint_sha1 = project.fingerprint_sha1
  if(file("signing.properties").exists()) file("signing.properties").withInputStream { sp.load(it) }
  return sp
}();
task signJar(type: SignJar, dependsOn: jar) {
  onlyIf { signing.hasProperty("keystore_file") }
  if(signing.hasProperty("keystore_file")) {
    keyStore = signing.getProperty("keystore_file")
    alias = signing.getProperty("keystore_alias")
    storePass = signing.getProperty("keystore_pass")
    keyPass = signing.getProperty("keystore_keypass")
    inputFile = jar.archivePath
    outputFile = jar.archivePath
  }
}
build.dependsOn signJar

publishing {
  publications { mavenJava(MavenPublication) { artifact reobfArtifact } }
  repositories { maven { url "file:///${project.projectDir}/mcmodsrepo" } }
}
